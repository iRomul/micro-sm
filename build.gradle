plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
}

group 'io.github.iromul.microsm'
version '0.1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    testImplementation 'org.assertj:assertj-core:3.11.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

test {
    useJUnitPlatform()
}

task sourcesJar(type:Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type:Jar, dependsOn:[javadoc]) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenAll(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Micro SM'
                description = 'Simple state machine implementation'
                url = 'https://github.com/iRomul/micro-sm'

                developers {
                    developer {
                        id = 'iRomul'
                        name = 'Roman Komarevtsev'
                        email = 'hellromul@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git@github.com:iRomul/micro-sm.git'
                    developerConnection = 'scm:git:git@github.com:iRomul/micro-sm.git'
                    url = 'https://github.com/iRomul/micro-sm'
                }
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

check.dependsOn jacocoTestReport
